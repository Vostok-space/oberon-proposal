# Среда Oberon с высокоуровневой защитой памяти
Версия: 0.2

Это предложение позволяет создать полностью защищённую по памяти высокоуровневую программную среду, которую нельзя взломать изнутри только с помощью её собственных средств. Лазейки, необходимые в низкоуровневом коде, здесь ограничены таким образом, чтобы позволить им выполнять полезную часть своей работы, тем не менее запрещая неподконтрольный доступ.

Суть предложения не привязана к Oberon-07[^0], но избегая лишней абстрактности для простоты приведена здесь в виде, приближенном к этому языку.

### 0. Отказ от псевдомодуля SYSTEM
Псевдомодуль SYSTEM[^1] из определения языка используется как простое средство для небезопасного обращения к памяти, чтобы не прибегая к низкоуровневым языкам, подобным машинному, покрыть потребности всей системы в некотором количестве низкоуровневого кода, в частности, для распределителя памяти.

Однако неограниченная доступность SYSTEM внутри языка, во многом, обесценивает гарантии по памяти, которые способна дать остальная часть языка Oberon, что имеет критическое значение при наличии недоверенного кода. В то же время SYSTEM может использоваться и для задач, для которых неограниченный доступ к памяти является избыточным.

### 1. Псевдомодуль для работы с памятью
Специфичный для нижележащей платформы, игнорирующий тип переменной, кроме её пределов. Частично совместим с оригинальным псевдомодулем SYSTEM. Не предоставляет прямой доступ к переменным, которые являются указателями и адресами или содержат их *(стоит отличать содержание в себе от ссылаемости)*.

Предоставляет тип-адрес только для чтения данных и тип-адрес также для записи. Разновидность адреса зависит от того, ограничена ли переменная на запись или нет согласно основным правилам языка. Адрес с возможностью записи может быть присвоен переменной адреса только для чтения, но не наоборот. Оба типа применимы только в качестве непосредственного типа локальных переменных и формальных параметров неэкспортированных процедур. Адрес может быть использован там, где ожидается массив байт.

Следующая спецификация определяет сразу два имени псевдомодуля для чёткого разграничения кода, работающего с адресами только полностью открытых переменных согласно правилам языка (только через PSYSTEM *(Proper)*), и обращающегося к скрытым переменным (только через XSYSTEM *(X-ray)*).

<pre><code>DEFINITION PSYSTEM,<b>XSYSTEM</b>;

 TYPE
  ADDR;
    ASSERT(Readable(ADDR   ) & ~Writable(ADDR));
  VARADDR;
    ASSERT(Readable(VARADDR) & Writable(VARADDR));
  Addr- = ADDR | VARADDR;<i>(*«-» означает, что описание локально*)</i>
  FORALL(var: Addr)
    ASSERT(IsLocalVar(var) OR IsFormalParam(var))
  END;
  FORALL(asBytes: Addr; VAR par: ARRAY OF BYTE)
    ASSERT(Assignable(par, asBytes)) <i>(* par := asBytes *)</i>
  END;
  FORALL(asBytes: VARADDR; par: ARRAY OF BYTE)
    ASSERT(Assignable(asBytes, par)) <i>(* asBytes := par *)</i>
  END;
  FORALL(ro: ADDR; rw: VARADDR)
    ASSERT(Assignable(ro, rw)
        & ~Assignable(rw, ro))
  END;

  AnyWithoutPointers- = AnyType
   ASSERT(~HasPointers(AnyWithoutPointers));

 PROCEDURE ADR(v: AnyWithoutPointers): (adr: Addr)
  ASSERT(HasPrivateContent(v) = (DEFINITION.NAME = <b>XSYSTEM</b>))
  ASSERT(SIZE(v) = LEN(adr))
  ASSERT(IsVar(v) = (adr IS VARADDR));

 PROCEDURE ADD(a: Addr; ofs: INTEGER): Addr
  ASSERT(ofs ≤ LEN(a));

 PROCEDURE SIZE(T: AnyType): INTEGER;
 PROCEDURE BITSIZE(t: AnyType): INTEGER;
 PROCEDURE BIT(a: Addr; n: INTEGER): BOOLEAN
  ASSERT(0 ≤ n &lt; BITSIZE(INTEGER));

 PROCEDURE GET(a: Addr; VAR v: AnyBasicType)
  ASSERT(LEN(a) ≤ SIZE(v));

 PROCEDURE PUT(a: VARADDR;  v: AnyBasicType)
  ASSERT(LEN(a) ≥ SIZE(v));

 PROCEDURE COPY(src: Addr; dst: VARADDR; n: INTEGER)
  ASSERT(0 ≤ n ≤ LEN(src))
  ASSERT(n ≤ LEN(dst));

 PROCEDURE VAL(T: Scalar; adr: Addr): T
  ASSERT(SIZE(T) ≤ LEN(adr));

END PSYSTEM,<b>XSYSTEM</b>.</code></pre>

Доступ к импорту как XSYSTEM, так и PSYSTEM должен быть ограничен.

### 2. Полный доступ к памяти
Полный доступ к памяти осуществляется за счёт псевдомодуля для создания указателей и произвольных адресов. Доступ к нему должен даваться только самой узкой прослойке системообразующих модулей. В первую очередь, это код управления и распределения памяти, а также некоторые драйвера.

Конкретная сигнатура псевдомодуля может отличаться для разных нижележащих платформ. Например, не для всех машинных языков адрес — это смещение в массиве байт памяти и может нести более сложный смысл; платформа может иметь несколько типов памяти, и так далее.

<pre><code>DEFINITION SYSTEMADDRESS;

 TYPE
  POINTER; <i>(* совместим с любым указателем *)</i>
  VARADDR; <i>(* совместим с любым адресом из (P|X)SYSTEM *)</i>
  INDEX;   <i>(* cмещение в памяти, константы всегда задаются 16-рично *)</i>

  AddrPtr- = VARADDR | POINTER;

 PROCEDURE ABSOLUTE(ofs: INDEX): VARADDR;
 PROCEDURE MAKEPTR (ofs: INDEX): POINTER;

 PROCEDURE OFS(ptr: AddrPtr): INDEX;

 PROCEDURE ADD(v, w: INDEX): INDEX;
 PROCEDURE SUB(v, w: INDEX): INDEX;
 PROCEDURE MUL(v, w: INDEX): INDEX;
 PROCEDURE DIV(v, w: INDEX): INDEX;
 PROCEDURE MOD(v, w: INDEX): INDEX;
 <i>(* (v <b>op</b> w) MOD 2<sup>BITSIZE(v)</sup> *)</i>
 PROCEDURE ADDMOD(v, w: INDEX): INDEX;
 PROCEDURE SUBMOD(v, w: INDEX): INDEX;
 PROCEDURE MULMOD(v, w: INDEX): INDEX;

 PROCEDURE XOR(v, w: INDEX): INDEX;
 PROCEDURE OR (v, w: INDEX): INDEX;
 PROCEDURE AND(v, w: INDEX): INDEX;

 PROCEDURE SHL(v: INDEX; n: INTEGER): INDEX
 PROCEDURE SHR(v: INDEX; n: INTEGER): INDEX
 PROCEDURE ROR(v: INDEX; n: INTEGER): INDEX
  ASSERT(0 ≤ n &lt; BITSIZE(v));

 PROCEDURE LT(v, w: INDEX): BOOLEAN;
 PROCEDURE EQ(v, w: INDEX): BOOLEAN;
 PROCEDURE LE(v, w: INDEX): BOOLEAN;

END SYSTEMADDRESS.</code></pre>

### 3. Защита от нарушений всей системы
Полноценная защита памяти развитой высокоуровневой системы подразумевает обязательную защиту не только оперативной, но также и непрерывной памяти, используемой для хранения исполняемого кода.

(0) Генератор машинного кода — нижняя часть транслятора системного высокоуровневого языка, (1) распределитель оперативной памяти, (2) драйвер хранилища — это те составляющие высокоуровневой системы, которые несут главную ответственность за целостность памяти в целом, но не могут быть ограничены в той же мере, как все остальные модули. Такая часть ПО должна наделяться особой ролью и входить в доверенное ядро системы. Весь остальной код может быть недоверенным, но из-за предлагаемых ограничений он всё равно не будет способен нарушить установленные границы.

Важным требованием для целостности является доказанная или, по меньшей мере, предельно испытанная правильность кода указанных доверенных подсистем (0-2).

### 4. Вспомогательные ограничения
Следующие ограничения не являются необходимыми для сохранения целостности, но способствуют улучшению архитектуры кода и могут уменьшить вероятность появления ошибок.

#### 4.0 Запрет псевдонимов
Среда программирования не должна позволять задавать псевдонимы для системных псевдомодулей при их импорте.

#### 4.1 Процедуры-посредники PSYSTEM|XSYSTEM
Это архитектурное ограничение, не позволяющее напрямую обращаться к \*SYSTEM пользовательскому коду. Это усложняет злоупотребление, вынуждая создавать ограниченные обёртки, через которые легче контролировать обращения к \*SYSTEM, чем через избыточные прямые обращения к полному набору возможностей.

Аналогия — машина не разбирает себя по собственной инициативе, повышая тем самым вероятность поломки по мере разбора, а обрабатывается посредником, распоряжающимся только тем, что ему было предоставлено.

 0. Разрешено получать адрес только от формальных параметров. Процедуры, которые делают это, являются посредниками
 1. Пользовательский код должен вызвать процедуру-посредника, передавая ей любую переменную для разбора
 2. Процедура—посредник может вызывать других посредников только применительно к своим формальным параметрам
 3. Тип формального параметра в процедуре-посреднике может быть задан как перечисление допустимых типов. Такой тип задаётся без метки экспорта в подразделе TYPE модуля, и применим только для формального параметра посредника. К такому параметру неприменимы обычные операции, а только операции по взятию адреса

### 5. Пример
<pre><code>MODULE Vector;
 IMPORT PSYSTEM, Hash;

 TYPE
  Vector* = RECORD x*, y*, z*: INTEGER END;
  Sets*   = RECORD l*, r*: SET END;
  List*   = POINTER TO RECORD v*: Vector; next: List END;
  <i>(* Ошибка — адрес не оборачивается
  Wrapper = RECORD adr: PSYSTEM.ADDR END *)</i>

  VectorOrSets = Vector | Sets;

 PROCEDURE CalcVectorHash*(v: VectorOrSets): INTEGER;
 VAR bytes: PSYSTEM.ADDR;
 BEGIN
  bytes := PSYSTEM.ADR(v)
 RETURN Hash.Calc(bytes, 0, LEN(bytes))
 END CalcVectorHash;

 PROCEDURE CalcListHash*(l: List): INTEGER;
 VAR h: INTEGER;
  PROCEDURE Next(VAR h: INTEGER; v: Vector);
  BEGIN
    Hash.Next(h, PSYSTEM.ADR(v))
  END Next;
 BEGIN
  Hash.Begin(h);
    WHILE l # NIL DO
      Next(h, l.v); l := l.next
    END;
  Hash.End(h)
 RETURN h
 END CalcListHash;

 PROCEDURE Err(VAR v: Vector);
 VAR v1: Vector; a: PSYSTEM.ADDR;
 BEGIN
  a := PSYSTEM.ADR(v);
  <i>(* Ошибка, v1 — локальная переменная, а не формальный параметр:
  a := PSYSTEM.ADR(v1); *)</i>
 END Err;

END Vector.</code></pre>


[^0]: [Сообщение о языке Oberon](https://vostok-space.github.io/Oberon-report/oberon.html)
[^1]: [Определение псевдомодуля SYSTEM](https://vostok-space.github.io/Oberon-report/oberon.html#11.1)
